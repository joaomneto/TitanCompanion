import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    ext.kotlin_version = '1.2.30'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
        classpath 'com.amazonaws:aws-devicefarm-gradle-plugin:1.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'

dependencies {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation 'com.android.support:support-v4:27.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'commons-io:commons-io:2.6'
    implementation 'androidx.core:core-ktx:0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.8"
    testImplementation "org.robolectric:shadows-support-v4:3.3.2"
    testImplementation "io.mockk:mockk:1.8.1"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.18.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'

}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    dataBinding {
        enabled = true
    }

    if (project.hasProperty('RELEASE_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        if (project.hasProperty('RELEASE_STORE_FILE')) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jvmArgs '-noverify'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            renderscript.srcDirs = ['src/main/java']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDir 'src/androidTest/java'
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

if (project.hasProperty('SERVICE_ACCOUNT_EMAIL')) {
    play {
        track = 'beta'
        serviceAccountEmail = SERVICE_ACCOUNT_EMAIL
        pk12File = file(SERVICE_ACCOUNT_SECRET_FILE)
    }
}

repositories {
    mavenCentral()
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                    TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        exceptionFormat = info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) {
                println('-----------')
                println("${result.resultType}")
                println("Total: ${result.testCount}")
                println("Passed: ${result.successfulTestCount}")
                println("Failed: ${result.failedTestCount}")
                println("Skiped: ${result.skippedTestCount}")
                println('-----------')
            }
        }
    }
}

